plugins {
    id 'com.android.library'
    id("org.jetbrains.dokka") version "1.5.0"
}
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android.buildFeatures.buildConfig true

android {
    compileSdk = 34
    buildToolsVersion = "34.0.0"

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        // The hack below is because VERSION_NAME has been removed from BuildConfig as of Gradle Plugin 4.1
        def addConstant = { constantName, constantValue ->
            manifestPlaceholders += [(constantName): constantValue]
            buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }
        addConstant("VERSION_NAME", version)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        abortOnError false
    }

    namespace 'org.altbeacon.beacon'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0' // 1.6.1 requires targeting android SDK 33
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    testImplementation 'junit:junit:4.13.2'

    testImplementation 'org.robolectric:robolectric:4.9'
    testImplementation 'com.google.android:android-test:4.1.1.4'
    testImplementation 'com.squareup:fest-android:1.0.8@aar'
    testImplementation 'org.mockito:mockito-core:2.23.4'

    androidTestUtil 'androidx.test:orchestrator:1.4.2'

    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'org.apache.commons:commons-math3:3.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation "androidx.core:core-ktx:1.3.2" // 1.10.1 requires targeting android SDK 33
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    dokkaHtmlPlugin("org.jetbrains.dokka:kotlin-as-java-plugin:1.5.0")
}

ext {
    PUBLISH_GROUP_ID = 'org.altbeacon'
    PUBLISH_VERSION = "2.20.30"
    PUBLISH_ARTIFACT_ID = 'android-beacon-library'
}

def githubProperties = new Properties()
def localCredentials = rootProject.file("github.properties")
if (localCredentials.exists()) {
    githubProperties.load(new FileInputStream(localCredentials))
}

publishing {
    publications {
        release(MavenPublication) {
            groupId PUBLISH_GROUP_ID // Replace with group ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION
            artifact("$buildDir/outputs/aar/${PUBLISH_ARTIFACT_ID}-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                def configs = ["implementation", "api"]
                for (configName in configs) {
                    configurations.getByName(configName).allDependencies.each {
                        if (it.group != null && (it.name != null || "unspecified".equals(it.name)) && it.version != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            /** Configure path of your package repository on Github
             *  Replace GITHUB_USERID with your/organisation Github userID and REPOSITORY with the repository name on GitHub
             */
            url = uri("https://maven.pkg.github.com/humblerookie/android-beacon-library")

            credentials {
                /**Create github.properties in root project folder file with gpr.usr=GITHUB_USER_ID  & gpr.key=PERSONAL_ACCESS_TOKEN**/
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}
